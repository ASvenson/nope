plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'net.minecrell.licenser' version '0.4.1'
}

// For sortClassFields
apply plugin: 'org.spongepowered.gradle'

archivesBaseName = 'nope'
group = 'com.minecraftonline'
version = '0.1.0'
description = "A region protection plugin"

ext.spongeVersion = '7.2.0'

sponge.plugin.meta {
  authors = ['PietElite', 'tyhdefu', '14mRh4X0r']
  url = "https://www.minecraftonline.com"
}

repositories {
    maven { url "http://maven.sk89q.com/repo/" }
}

dependencies {
    implementation "org.spongepowered:spongeapi:${spongeVersion}"
    annotationProcessor "org.spongepowered:spongeapi:${spongeVersion}"

    implementation 'com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT'
    implementation 'com.sk89q.worldedit:worldedit-sponge:6.1.7-SNAPSHOT'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
}

// Taken from https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests
sourceSets {
    settingDescription {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
  settingDescriptionImplementation.extendsFrom testImplementation
}

sortClassFields {
    add 'com.minecraftonline.nope.control.Settings'
}

compileJava.dependsOn sortClassFields

task generateSettingDescriptions(type: Test) {
    testClassesDirs = sourceSets.settingDescription.output.classesDirs
    classpath = sourceSets.settingDescription.runtimeClasspath
}

build.finalizedBy generateSettingDescriptions

